import groovy.json.JsonBuilder

class AndroidHelper {

    static def saveProcessedConfig(project, config) {
        def path = project.file("assets/config")
        path.mkdirs()
        def jsonWriter = new FileWriter(path.absolutePath + "/config.json")
        def builder = new JsonBuilder(config)
        jsonWriter.withWriter {
            builder.writeTo(it)
        }
    }

    static def saveGoogleServiceJson(project, firebaseConfig) {
        def googleServiceJsonPath = project.projectDir.toString() + "/google-services.json"
        new FileWriter(googleServiceJsonPath).withWriter {
            getGoogleServicesContent(firebaseConfig).writeTo(it)
        }
    }

    static def getGoogleServicesContent(firebaseConfig) {
        if (checkRequiredFields(firebaseConfig)) {
            def jsonBuilder = new JsonBuilder()
            def projectNumber = firebaseConfig.get("PROJECT_NUMBER")
            def projectId = firebaseConfig.get("PROJECT_ID")
            def oauthClientId = firebaseConfig.get("OAUTH_CLIENT_ID")
            def currentKey = firebaseConfig.get("CURRENT_KEY")
            def otherPlatformOauthClientId = firebaseConfig.get("OTHER_PLATFORM_OAUTH_CLIENT_ID")
            def appStoreId = firebaseConfig.get("APP_STORE_ID")
            // construct the google-services.json fields
            jsonBuilder {
                project_info {
                    project_number projectNumber
                    firebase_url "https://$projectId" + ".firebaseio.com"
                    project_id projectId
                    storage_bucket projectId + ".appspot.com"
                }
                client([{
                            client_info {
                                "mobilesdk_app_id" "1:$projectNumber:android:c04089bb49270266"
                                "android_client_info" {
                                    "package_name" "org.edx.mobile"
                                }
                            }
                            oauth_client([
                                    {
                                        "client_id" oauthClientId
                                        "client_type" 3
                                    }
                            ])
                            api_key(
                                    [{
                                         "current_key" currentKey
                                     }])
                            services {
                                appinvite_service {
                                    other_platform_oauth_client([{
                                                                     "client_id" oauthClientId
                                                                     "client_type" 3
                                                                 },
                                                                 {
                                                                     "client_id" otherPlatformOauthClientId
                                                                     "client_type" 2
                                                                     ios_info {
                                                                         "bundle_id" "org.edx.mobile"
                                                                         "app_store_id" appStoreId
                                                                     }
                                                                 }
                                    ])
                                }
                            }
                        }])
                configuration_version "1"
            }
            return jsonBuilder
        }
    }

    static def checkRequiredFields(firebaseConfig) {
        def available = true
        def message = ""
        if (!firebaseConfig.get("PROJECT_NUMBER")) {
            message += 'FIREBASE:PROJECT_NUMBER is missing or empty\n'
            available = false
        }
        if (!firebaseConfig.get("PROJECT_ID")) {
            message += 'FIREBASE:PROJECT_ID is missing or empty\n'
            available = false
        }

        if (!firebaseConfig.get("OAUTH_CLIENT_ID")) {
            message += 'FIREBASE:OAUTH_CLIENT_ID is missing or empty\n'
            available = false
        }

        if (!firebaseConfig.get("CURRENT_KEY")) {
            message += 'FIREBASE:CURRENT_KEY is missing or empty\n'
            available = false
        }

        if (!firebaseConfig.get("OTHER_PLATFORM_OAUTH_CLIENT_ID")) {
            message += 'FIREBASE:OTHER_PLATFORM_OAUTH_CLIENT_ID is missing or empty\n'
            available = false
        }
        if (!firebaseConfig.get("APP_STORE_ID")) {
            message += 'FIREBASE:APP_STORE_ID is missing or empty\n'
            available = false
        }
        if (!available) {
            throw new GradleException(message)
        }
        return available
    }
}

ext.AndroidHelper = AndroidHelper
